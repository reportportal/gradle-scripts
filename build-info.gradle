buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.ajoberstar:grgit:1.7.2'

    }
}

task gitInfo(type: Copy) {
    def grgit = org.ajoberstar.grgit.Grgit.open(dir: project.projectDir.toString())
    def branch = grgit.branch.getCurrent().getName() + '-' + grgit.head().id
    def repo = grgit.remote.list().collect { remote ->
        def url = remote.url
        def gitPostfix = url.indexOf(".git")
        return url.substring(url.indexOf(":") + 1, gitPostfix == -1 ? url.length() : gitPostfix)
    }.unique(false).getAt(0)

    project.properties.ext.set("branch", branch)
    project.properties.ext.set("repo", repo)

    from('src/main/resources') {
        filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: project.properties.findAll {
            it.value instanceof String
        }.collectEntries {
            [it.key, it.value]
        })
    }
    into "$buildDir/resources/main"
    include '**/application.yaml'

    outputs.upToDateWhen { false }
}
