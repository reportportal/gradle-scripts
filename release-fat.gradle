buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:5.2.0"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4"
        classpath "net.researchgate:gradle-release:2.8.1"
    }
}

apply plugin: 'maven-publish'
apply plugin: 'maven'
apply plugin: 'java'
apply plugin: com.github.jengelman.gradle.plugins.shadow.ShadowPlugin
apply plugin: com.jfrog.bintray.gradle.BintrayPlugin
apply plugin: net.researchgate.release.ReleasePlugin

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

javadoc {
    failOnError = false
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives shadowJar
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
            
            artifact project.tasks.javadocJar
            artifact project.tasks.sourcesJar

            pom.withXml {
                def root = asNode()
                root.dependencies.'*'.findAll() {
                    it.scope.text() == 'runtime' && project.configurations.shadow.allDependencies.find { dep ->
                        dep.name == it.artifactId.text()
                    }
                }.each() {
                    it.scope*.value = 'compile'
                }
            }

            groupId 'com.epam.reportportal'
            artifactId "$project.name"

        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['shadow']
    publish = true
    pkg {
        repo = 'reportportal'
        name = "$project.name"
        desc = "$project.description"
        websiteUrl = 'https://reportportal.github.io/'
        issueTrackerUrl = "https://github.com/reportportal/$project.name/issues"
        vcsUrl = "https://github.com/reportportal/$project.name"
        githubRepo = "reportportal/$project.name"
        githubReleaseNotesFile = 'CHANGELOG.md'

        userOrg = 'epam'
        licenses = ['Apache-2.0']
    }
}
afterReleaseBuild.dependsOn bintrayUpload
